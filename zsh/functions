# -----------------------------------
# Source Plugins
# -----------------------------------
Plugin() {
    source ~/.dotfiles/zsh/plugins/$1
}

# -----------------------------------
# Setup Prompt
# -----------------------------------
PromptSetup() {
    autoload -U colors && colors
    autoload -Uz vcs_info
    zstyle ':vcs_info:*' enable git svn
    zstyle ':vcs_info:git:*' formats " %b%"

    # Handle Virtual ENV
    virtualenv_info() { 
        [ $VIRTUAL_ENV ] && echo '('`basename $VIRTUAL_ENV`') '
    }
    export VIRTUAL_ENV_DISABLE_PROMPT=1

    # Main Prompt
    main_prompt() {
        if [[ "$USER" == "adamwolf" ]]; then
            PROMPT="%B%{$fg[green]%}[wolfy] ▶ %{$fg[cyan]%}%c%{$reset_color%} "
            PROMPT+="%{$fg[yellow]%}$(virtualenv_info)%{$reset_color%}"
        else
            PROMPT="%B%{$fg[green]%}[%n] ▶ %{$fg[cyan]%}%c%{$reset_color%} "
        fi
    }

    precmd() {
        main_prompt
        
        if [ "$(git rev-parse --is-bare-repository 2>/dev/null)" = "true" ]; then
            WORK_TREES=$(( $(git worktree list | wc -l) - 1 ))
            RPROMPT="%{$bg[yellow]%}%{$fg_bold[black]%}  ${WORK_TREES} %{$reset_color%} "
            return
        fi

        vcs_info

        if [[ -n ${vcs_info_msg_0_} ]]; then
            STATUS=$(command git status --porcelain 2> /dev/null | tail -n1)
            if [[ -n $STATUS ]]; then
                RPROMPT="%{$bg[red]%}%{$fg_bold[black]%} ${vcs_info_msg_0_}  %{$reset_color%} "
            else
                RPROMPT="%{$bg[green]%}%{$fg_bold[black]%} ${vcs_info_msg_0_}  %{$reset_color%} "
            fi
        else
            RPROMPT=""
        fi
    }
    setopt prompt_subst

    # Setup Bindings
    Bindings
}

# -----------------------------------
# Setup History
# -----------------------------------
SetupHistory() {
    HISTFILE=~/.cache/zsh/history
    HISTSIZE=10000
    SAVEHIST=10000
    HISTTIMEFORMAT="[%F %T] "
    setopt EXTENDED_HISTORY
    setopt SHARE_HISTORY
}

# -----------------------------------
# Check for Updates
# -----------------------------------
checkUpdates() {
    echo "$fg[blue]Checking for updates"
    cd ~/.dotfiles
    git fetch --quiet

    # Check if up to date
    if git merge-base --is-ancestor origin/master master; then
        echo "$fg[green]Up to date!"
        sleep 1
        cd - > /dev/null
        clear
    else
        updateConfirmation
        cd - > /dev/null
        clear
    fi
}

# -----------------------------------
# Update Confirmation
# Update / Continue
# -----------------------------------
updateConfirmation() {
    if read -q "choice?$fg[blue]Updates available. Would you like to continue? (y/n):$reset_color "; then
        echo "\n$fg[cyan]Updating Configs"

        # Pull updated dotfiles
        git pull --quiet origin master

        # Update nvim plugins
        echo "$fg[cyan]Installing Plugins"
        nvim -E -s -u "~/.dotfiles/nvim/init.lua" +PlugInstall +qall

        # Finished
        echo "$fg[green]Done!"
        sleep 5
        clear
        source ~/.zshrc
    fi
}

# -----------------------------------
# Helper Functions
# -----------------------------------
ec() {
    if [ -f $1 ]; then
        case $1 in
            *.tar.bz2)
                FILENAME=$1
                mkdir -p "${FILENAME%%.*}" 
                tar xjf $1 -C "${FILENAME%%.*}"
                ;;
            *.tar.gz)
                FILENAME=$1
                mkdir -p "${FILENAME%%.*}" 
                tar xzf $1 -C "${FILENAME%%.*}"
                ;;
            *.tar)
                FILENAME=$1
                mkdir -p "${FILENAME%%.*}" 
                tar xf $1  -C "${FILENAME%%.*}"
                ;;
            *.tbz2)
                FILENAME=$1
                mkdir -p "${FILENAME%%.*}" 
                tar xjf $1 -C "${FILENAME%%.*}"
                ;;
            *.rar)
                FILENAME=$1
                mkdir -p "${FILENAME%%.*}" 
                unrar x $1 "${FILENAME%%.*}"
                ;;
            *.zip)
                FILENAME=$1
                mkdir -p "${FILENAME%%.*}" 
                unzip $1 -d "${FILENAME%%.*}"
                ;;
            *)          printf "\033[31m'$1' cannot be extracted\033[0m\n" ;;
        esac
    else
        printf "\033[31m'$1' is not a valid file\033[0m\n"
    fi
}

mkcmp() {
    if [ $# -eq 2 ]; then
        case $1 in
            bz2) tar -cvjf $2.tar.bz2 $2 ;;
            gz) tar -czvf $2.tar.gz $2 ;;
            rar) rar a $2.rar $2 ;;
            zip) zip -r $2.zip $2 ;;
            *) printf "\033[31m'$1' format not supported\033[0m\n" ;;
        esac
    else
        printf "\033[31mInvalid Syntax\033[0m\n"
        printf "Format should be: \033[32mmkcmp \033[36mformat \033[33mfolder\033[0m\n"
    fi
}

large() {
    sudo find / -xdev -type f -size +$1 -exec grc ls -Salh --color {} +
}

ff() {
    # fdfind --type f --hidden --exclude .git | \
    fd --type f --hidden --exclude .git | \
    fzf-tmux -p --preview="cat {}" --preview-window=right:40%:wrap --exit-0 \
    --color=dark \
    --color=fg:-1,bg:-1,hl:#c678dd,fg+:#ffffff,bg+:#4b5263,hl+:#98c379 \
    --color=info:#98c379,prompt:#61afef,pointer:#e5c07b,marker:#e5c07b,spinner:#61afef,header:#61afef | \
    xargs -r nvim
}

worktree_add() {
    git worktree add $1

    # If in travefy repo, add host to devcontainer .env
    if [[ $PWD == *"travefy.git"* ]]; then
        echo "HOST_OS=$OSTYPE" > "$1/.devcontainer/.env"
    fi

    cd $1
}

Bindings() {
    autoload edit-command-line; zle -N edit-command-line
    bindkey '^e' edit-command-line
}

# vim: set filetype=zsh :
